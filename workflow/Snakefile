#This workflow aligns a query genome assembly against a reference assembly.
#To manage the large size of the wheat genome, the full query assembly is aligned to a single chromosome of the reference assembly.

import os

QUERY_NAME = config['QUERY_NAME']
REF_NAME = config['REF_NAME']
QUERY_FASTA = config['QUERY_FASTA']
REF_FASTA = config['REF_FASTA']
CHRS = config['CHRS']

rule all:
    input:
        expand("results/ref_chrs/{chr}.fa", chr = CHRS), 
        expand("results/plots/chr_full/{ref}_{query}_{chr}.png", ref = REF_NAME, query = QUERY_NAME, chr = CHRS), 
        expand("results/plots/chr_chr/{ref}_{query}_{chr}.png", ref = REF_NAME, query = QUERY_NAME, chr = CHRS)

rule subset_ref_chrs:
    input:
        REF_FASTA
    output:
        "results/ref_chrs/{chr}.fa"
    log:
        "logs/subset_ref_{chr}.log"
    shell:
        "seqkit grep -rp '{wildcards.chr}' {input} > {output} 2> {log}"

rule nucmer:
    input:
        ref = "results/ref_chrs/{chr}.fa", 
        sample = QUERY_FASTA
    output:
        expand("results/nucmer/{ref}_{query}_{{chr}}.delta", ref = REF_NAME, query = QUERY_NAME)
    params:
        prefix = expand("results/nucmer/{ref}_{query}_{{chr}}", ref = REF_NAME, query = QUERY_NAME), 
        maxgap = "500", 
        mincluster = "100"
    log:
        "logs/nucmer_{chr}.log"
    threads: 12
    shell:
        "nucmer --prefix {params.prefix} "
        "--maxgap={params.maxgap} "
        "--mincluster={params.mincluster} "
        "--threads={threads} "
        "{input.ref} "
        "{input.sample} "
        "2> {log}"

rule filter_delta:
    input:
        expand("results/nucmer/{ref}_{query}_{{chr}}.delta", ref = REF_NAME, query = QUERY_NAME)
    output:
        expand("results/filter/{ref}_{query}_{{chr}}.delta", ref = REF_NAME, query = QUERY_NAME)
    log:
        "logs/filter_{chr}.log"
    threads: 12
    shell:
        "delta-filter -l 5000 -i 99 -u 99 {input} > {output} 2> {log}"

rule plotscript_filtered_delta:
    input:
        expand("results/filter/{ref}_{query}_{{chr}}.delta", ref = REF_NAME, query = QUERY_NAME)
    output:
        gp_full = expand("results/plots/chr_full/{ref}_{query}_{{chr}}.gp", ref = REF_NAME, query = QUERY_NAME), 
        gp_chr = expand("results/plots/chr_chr/{ref}_{query}_{{chr}}.gp", ref = REF_NAME, query = QUERY_NAME)
    params:
        prefix_full = expand("results/plots/chr_full/{ref}_{query}_{{chr}}", ref = REF_NAME, query = QUERY_NAME), 
        prefix_chr = expand("results/plots/chr_chr/{ref}_{query}_{{chr}}", ref = REF_NAME, query = QUERY_NAME), 
        chr = "{chr}_RagTag", 
        title = expand("{query} (y) aligned to {ref} (x)", query = QUERY_NAME, ref = REF_NAME)
    threads: 12
    shell:
        "mummerplot --png -large -title '{params.title}' -p {params.prefix_full} {input} ; "
        "mummerplot --png -large -title '{params.title}' -p {params.prefix_chr} -q {params.chr} {input}"

rule gnuplot_filtered_delta:
    input:
        gp_full = expand("results/plots/chr_full/{ref}_{query}_{{chr}}.gp", ref = REF_NAME, query = QUERY_NAME), 
        gp_chr = expand("results/plots/chr_chr/{ref}_{query}_{{chr}}.gp", ref = REF_NAME, query = QUERY_NAME)
    output:
        gp_full = expand("results/plots/chr_full/{ref}_{query}_{{chr}}_edit.gp", ref = REF_NAME, query = QUERY_NAME), 
        gp_chr = expand("results/plots/chr_chr/{ref}_{query}_{{chr}}_edit.gp", ref = REF_NAME, query = QUERY_NAME), 
        plot_full = expand("results/plots/chr_full/{ref}_{query}_{{chr}}.png", ref = REF_NAME, query = QUERY_NAME), 
        plot_chr = expand("results/plots/chr_chr/{ref}_{query}_{{chr}}.png", ref = REF_NAME, query = QUERY_NAME)
    shell:
        "sed 's/set terminal png tiny size 1400,1400/set terminal png large size 1000,1000/' {input.gp_full} > {output.gp_full} ; "
        "sed 's/set terminal png tiny size 1400,1400/set terminal png large size 1000,1000/' {input.gp_chr} > {output.gp_chr} ; "
        "gnuplot {output.gp_full} > {output.plot_full} ; "
        "gnuplot {output.gp_chr} > {output.plot_chr}"

